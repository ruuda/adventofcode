let graph = {
  for line in std.read_file_utf8("input.txt").split_lines():
  let parts = line.split(": ");
  parts[0]: parts[1].split(" "),

  // The out node is not explicitly present, we add it ourselves.
  "out": [],
};

// Now we toposort the graph.
let iter = (self, layers, g) =>
  let leaves = [
    for node, outs in g:
    if outs.len() == 0:
    node
  ];
  let g_new = {
    for node, outs in g:
    if outs.len() > 0:
    node: [
      for out in outs:
      if not leaves.contains(out):
      out
    ]
  };
  if leaves.len() > 0:
    self(self, [..layers, leaves ], g_new)
  else:
    [for layer in layers: ..layer ];

let toposort = iter(iter, [], graph);

// Build the inverse graph that contains per node, all nodes pointing into it.
let edges = {
  for node, outs in graph:
  for out in outs:
  { from = node, to = out }
};
let inverse_graph = {
  for node, edges in edges.group_by(e => e.to):
  node: [..edges.map(e => e.from) ]
};

let route_counts_part1 = toposort
  .reverse()
  .fold(
    // We start at "you" and have 1 way of reaching it.
    { "you": 1 },
    (counts, node) => (
      // The number of ways to reach this node.
      let n = inverse_graph.get(node, []).map(from => counts[from]).sum();
      { node: n, ...counts }
    ),
  );

// We count the number of ways to reach nodes, but in four ways:
// non: none, have not seen fft or dac yet
// fft: have only seen fft yet
// dac: have only seen dac yet
// don: done, seen both dac and fft.
let zero = { non = 0, dac = 0, fft = 0, don = 0 };
let vec_add = (n, m) => { for k, v in n: k: v + m[k] };
let vec_sum = xs => xs.fold(zero, vec_add);

let route_counts_part2 = toposort
  .reverse()
  .fold(
    {},
    (counts, node) =>
      let n = vec_sum(inverse_graph.get(node, []).map(from => counts[from]));
      {
        ...counts,
        node: {
          ...n,
          if node == "svr": non = 1,
          if node == "dac": ...{ dac = n.non, don = n.fft, non = 0 },
          if node == "fft": ...{ fft = n.non, don = n.dac, non = 0 },
        },
      },
  );

{
  part1 = route_counts_part1["out"],
  part2 = route_counts_part2["out"].don,
}
