let lines = std.read_file_utf8("input.txt").split_lines();
let cards = [
  for line in lines:
  // The "Card 1: " prefix is useless, the input is ordered. Cut off the prefix.
  let numbers_part = line.split(": ")[1];
  let wins_haves_str = numbers_part.split(" | ");
  let wins_haves = [
    for numbers in wins_haves_str: {
      for n in numbers.split(" "): if n != "": n.parse_int()
    }
  ];
  { wins = wins_haves[0], haves = wins_haves[1] }
];

let card_points = card => [
  for n in card.haves:
  if card.wins.contains(n):
  n
].fold(0, (score, n) => if score == 0 then 1 else score * 2);

{
  part1 = [for card in cards: card_points(card)].fold(0, (x, y) => x + y),
}
